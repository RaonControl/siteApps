program seqStep

option +r;

int spd;
double speed;
int jog;
int current;
int pwm;
int dir;
int stop;
int reset;
int relMove;
int absMove;
int relPulse;
int absPulse;
int jogCW;
int jogCCW;
int max;
int min;
int maxFlag;
int minFlag;

assign spd to "STEP:SET:SPD";
assign speed to "STEP:CVT:SPD";
assign current to "STEP:PLS:CUR";
assign pwm to "STEP:PWM";
assign dir to "STEP:DIR";
assign stop to "STEP:STOP";
assign reset to "STEP:RST";
assign relMove to "STEP:MOVR";
assign absMove to "STEP:MOVA";
assign relPulse to "STEP:PLS:REL";
assign absPulse to "STEP:PLS:ABS";

assign jogCW to "STEP:JCW";
assign jogCCW to "STEP:JCCW";

assign max to "STEP:PLS:MAX";
assign min to "STEP:PLS:MIN";
assign maxFlag to "STEP:MAXF";
assign minFlag to "STEP:MINF";

monitor reset;
monitor stop;
monitor relMove;
monitor absMove;

monitor jogCW;
monitor jogCCW;

int pulse;
int count;
int count_sign;
int limitCheck;

ss ssMain
{
  state init
  {
    when(delay(0.1))
    {
      pulse = 0;
      pwm = 0;
      current = 0;
      count = 0;
      count_sign = 0;
      relPulse = 0;
      absPulse = 0;

      pvPut(current);

      printf("Step Motor Sequence Start\n");

    } state Stop
  }

  state High
  {
    when(pulse == count || stop == 1 || maxFlag == 1 || minFlag == 1)
    {
      pvPut(maxFlag);
      pvPut(minFlag);
      
      pvPut(current);

      relMove = 0;
      pvPut(relMove);
 
      absMove = 0;
      pvPut(absMove);

      count = 0;

    } state Stop

    when(delay(speed))
    {
      pwm = 1;
      pvPut(pwm);

    } state Low
  }
  
  state Low
  {
    when(delay(speed))
    {
      pwm = 0;
      pvPut(pwm);

      count++;

      current += count_sign;

      if(current >= max) maxFlag = 1;      
      else if(current <= min) minFlag = 1;      

    } state High
  }

  state Stop
  {
    when(relMove == 1)
    {
      pvGet(speed);
      pvGet(relPulse);
      pvGet(min);
      pvGet(max);

      if(relPulse < 0) 
      {
        count_sign = -1;
        dir = 1;
      }
      else
      {
        count_sign = 1;
        dir = 0;
      }

      pulse = count_sign * relPulse;
      pvPut(dir);

      limitCheck = current + pulse * count_sign;

      if(limitCheck < max && limitCheck > min)
      {
        maxFlag = 0;
        minFlag = 0;

        pvPut(maxFlag);
        pvPut(minFlag);
      }

    } state High

    when(absMove == 1)
    {
      pvGet(speed);
      pvGet(absPulse);
      pvGet(min);
      pvGet(max);
      
      if(absPulse > current)
      {
        count_sign = 1;
        dir = 0;
      }
      else if(absPulse < current)
      {
        count_sign = -1;
        dir = 1;
      }
      else
      {
        count_sign = 0;
        pulse = count;
      }

      pulse = count_sign * (absPulse - current);
      pvPut(dir);
  
      limitCheck = current + pulse * count_sign;

      if(limitCheck < max && limitCheck > min)
      {
        maxFlag = 0;
        minFlag = 0;

        pvPut(maxFlag);
        pvPut(minFlag);
      }

    } state High

    when(jogCW == 1)
    { 
      pvGet(speed);
      pvGet(min);
      pvGet(max);

      dir = 1;
      count_sign = -1;
      pvPut(dir);

      maxFlag = 0;
      pvPut(minFlag);

    } state JogHigh

    when(jogCCW == 1)
    { 
      pvGet(speed);
      pvGet(min);
      pvGet(max);

      dir = 0;
      count_sign = 1;
      pvPut(dir);

      minFlag = 0; 
      pvPut(minFlag);
 
    } state JogHigh

    when(reset == 1)
    {
      maxFlag = 0;
      minFlag = 0;

      pvPut(maxFlag);
      pvPut(minFlag);

      current = 0;
      pvPut(current);

    } state Stop
  }

  state JogHigh
  {
    when((jogCW | jogCCW) == 0 || maxFlag == 1 || minFlag == 1)
    {
      pvPut(maxFlag);
      pvPut(minFlag);

      pvPut(current);

      count = 0;

    } state Stop

    when(delay(speed))
    {
      pwm = 1;
      pvPut(pwm);

    } state JogLow
  }
  
  state JogLow
  {
    when(delay(speed))
    {
      pwm = 0;
      pvPut(pwm);

      count++;

      current += count_sign;

      if(current >= max) maxFlag = 1;
      else if(current <= min) minFlag = 1;

    } state JogHigh
  }
}
